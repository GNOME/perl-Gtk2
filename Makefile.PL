#
# $Header$
#

use 5.008;
use strict;
use ExtUtils::MakeMaker;
use Cwd;
use blib '../G';
use blib '..';
use ExtUtils::Depends;
use G::PkgConfig;

require 'CodeGen.pm'; # other modules can just 'use Gtk2::CodeGen;' -- we
                      # can't because it's not installed yet.

my %pkgcfg = G::PkgConfig->find ('gtk+-2.0');

mkdir 'build', 0777;

#
# autogeneration
#
# we have a whole lot of types which need cast macros, typemap entries,
# and package mapping registration; instead of writing this by hand, we
# use a map file which defines how things are laid out, and then use
# some boilerplate code to convert that map into a header, typemap, and
# xsh file.  see the docs for Gtk2::CodeGen for more info.
#
# we'll use all the default filenames.
#
Gtk2::CodeGen->parse_maps ('gtk2perl');

# one more command generates code to boot all the various extra XS modules.
# we need this because we have lots of XS files without corresponding PMs to
# bootstrap them.
Gtk2::CodeGen->write_boot;

# this one turns the long list of #defines in gdkkeysyms.h into a large
# hash of key names and values, %Gtk2::Gdk::Keysyms.
system 'perl genkeysyms.pl > build/Keysyms.pm';

# now we're ready to start creating the makefile.
# we need to use ExtUtils::Depends to get relevant information out of
# the G extension, and to save config information for other modules which
# will chain from this one.

my $gtk2 = ExtUtils::Depends->new ('Gtk2', 'G');
# G added -I. for us, but we'll need to add -I./build so we can get to
# the autogenerated files.  we do this kindof as a public service to client
# modules, as these will carry through via the depends mechanism.
$gtk2->set_inc ($pkgcfg{cflags} . ' -I./build ');
$gtk2->set_libs ($pkgcfg{libs});
$gtk2->add_pm ('Gtk2.pm'          => '$(INST_LIBDIR)/Gtk2.pm',
               'build/Keysyms.pm' => '$(INST_LIBDIR)/Gtk2/Gdk/Keysyms.pm',
               'CodeGen.pm'       => '$(INST_LIBDIR)/Gtk2/CodeGen.pm',);
$gtk2->add_xs (<xs/*.xs>);
$gtk2->add_typemaps (map {cwd().'/'.$_} 'gdk.typemap', 'build/gtk2perl.typemap');
$gtk2->add_headers ('gtk2perl.h');

$gtk2->install (qw(gtk2perl.h build/gtk2perl-autogen.h));
$gtk2->save_config ('build/IFiles.pm');

WriteMakefile(
    NAME          => 'Gtk2',
    VERSION_FROM  => 'Gtk2.pm', # finds $VERSION
    PREREQ_PM     => {}, # e.g., Module::Name => 1.1
    ABSTRACT_FROM => 'Gtk2.pm', # retrieve abstract from module
    XSPROTOARG    => '-noprototypes',
    $gtk2->get_makefile_vars,
);

#
# there's a bug in the MakeMaker output which results in .xs files in a
# subdirectory being turned into .o files in this dir.  the bug is a missing
# -o on the compiler command line.  this is a hackish fix for it.
#
package MY;
sub const_cccmd {
	my $inherited = shift->SUPER::const_cccmd (@_);
	$inherited .= ' -o $@';
	$inherited;
}

#
# we need to use a module which will probably not be installed at test time.
# so, here's a nasty hack to add a few potential locations for G.pm and G.so
# to the call to test_harness.
#
sub test_via_harness {
	my $inherited = shift->SUPER::test_via_harness (@_);
	$inherited =~ s{test_harness\(\$\(\w+\),}{$& '../G/blib/lib', '../G/blib/arch', '../blib/lib', '../blib/arch',};
	$inherited;
}

package MAIN;
